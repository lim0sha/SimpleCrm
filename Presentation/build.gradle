plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'jacoco'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jacoco {
    toolVersion = "0.8.11"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':DataAccess')
    implementation project(':Types')
    implementation project(':Dto')
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation project(':Application')
}

bootJar {
    mainClass = 'com.simplecrm.Application.Application'
}

bootRun {
    doFirst {
        def envFile = file('.env')
        if (envFile.exists()) {
            envFile.eachLine { line ->
                if (line && !line.startsWith('#')) {
                    def (key, value) = line.split('=', 2)
                    if (key && value) {
                        systemProperty key.trim(), value.trim()
                        environment key.trim(), value.trim()
                    }
                }
            }
        }
    }
}


test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}